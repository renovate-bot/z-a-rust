# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et
#
# Copyright (c) 2021 Z-Shell Community
#
# FUNCTION: .za-bgn-bin-or-src-function-body
# Returns the body of the function wrapping a binary

.za-rust-bin-or-src-function-body() {

  local name="$2" bin="$3" dir="$4"
  integer run_type="$1" set_rust_home_vars="$5" \
  set_gem_home="$6" set_node_path="$7" set_cwd="$8" \
  use_all_null="$9" use_err_null="$10" use_out_null="$11"

  local nl=$'\n' tab=$'    '

  REPLY="function ${(q)name} {
    local bindir=${(qqq)bin:h}
    local -x PATH=${(qqq)dir}/bin:\"\$PATH\"
    ${${(M)set_rust_home_vars:#1}:+local -x RUSTUP_HOME=${(qqq)dir}/rustup CARGO_HOME=${(qqq)dir}}
    ${${(M)set_gem_home:#1}:+local -x GEM_HOME=${(qqq)dir}}
    ${${(M)set_node_path:#1}:+local -x NODE_PATH=${(qqq)dir}/node_modules}
    ${${(M)set_cwd:#1}:+"local oldpwd=${(qqq)PWD}
    () { setopt localoptions noautopushd; builtin cd -q ${(qqq)dir}; }"}
  "

  if (( run_type == 0 )); then
    REPLY+="$tab\"\$bindir\"/${(qqq)bin:t} \"\$@\""
  elif (( run_type == 1 )); then
    REPLY+="$tab() { source \"\$bindir\"/${(qqq)bin:t}; } \"\$@\""
  elif (( run_type == 2 )); then
    REPLY+="$tab() { eval \"\$(<\"\$bindir\"/${(qqq)bin:t})\"; } \"\$@\""
  fi

  (( use_all_null )) && REPLY+=" &>/dev/null"
  (( use_err_null )) && REPLY+=" 2>/dev/null"
  (( use_out_null )) && REPLY+=" >/dev/null"

  REPLY+="$nl"
  REPLY+="${${(M)set_cwd:#1}:+"$nl$tab() { setopt localoptions noautopushd; builtin cd -q \"\$oldpwd\"; }"}
  }"
}
